                .include "../submodules/beeb/include/beeb.s65"
                .include "shared_constants.s65"

*=$70
                .dsection zp
                .cerror *>$90


;-------------------------------------------------------------------------

; The !BOOT *EXEC encoding is super-inefficient, but 512 bytes can
; probably be guaranteed.
*=loader0_base
                .dsection code
                .cerror *>loader0_base+loader0_max_size

;-------------------------------------------------------------------------

                .section zp
                .endsection

;-------------------------------------------------------------------------
;
; Close !BOOT, then check for basic prerequisites and crap out if
; required. Then read track 0 side 1 (easy enough to do with OSWORD
; $72...), do a basic consistency check, then jump to its entry point.
                
                .section code

start:
                lda #$77        ; OSBYTE close any spool/exec files
                jsr osbyte

                ; The loader gets poked into memory from BASIC, so it
                ; could be running in the 2nd processor. Don't (yet?)
                ; bother trying to sort this out.
                lda #$ea        ; OSBYTE read/write Tube presence
                ldx #$00
                ldy #$ff
                jsr osbyte

                cpx #0
                beq tube_ok

                brk
                .byte 255
                .text "Not compatible with 6502 second processor"
                .byte 0

tube_ok:
                ; Cheap and cheerful Master detection.
                lda $fe34
                and #%11000000
                beq master_ok

                brk
                .byte 255
                .text "Requires Master 128/Master Compact"
                .byte 0

master_ok:
                ; Try to load loader1 from disk.
                ldx #<read_pblock
                ldy #>read_pblock
                lda #$72        ; OSWORD read/write ADFS sectors
                jsr osword

                lda read_pblock
                beq read_succeeded

                pha
                lsr a
                lsr a
                lsr a
                lsr a
                tax
                lda xdigits,x
                sta adfs_error_code+0
                pla
                and #$0f
                tax
                lda xdigits,x
                sta adfs_error_code+1
                
                brk
                .byte 255
                .text "ADFS read error: &"
adfs_error_code:
                .text "xx"
                .byte 0

read_succeeded:
                ldx #15
-
                lda loader1_base+loader1_size-16,x
                cmp expected_loader1_guid,x
                bne unexpected_guid
                dex
                bpl -

                ; Well, all looks vaguely in place.
                ;
                ; Enter ROM filing system.
                lda #$8d        ; OSBYTE select ROM filing system
                jsr osbyte
                
                ; claim the NMI.
                lda #$8f        ; OSBYTE issue paged ROM service request
                ldx #$0c        ; NMI claim
                ldy #$ff
                jsr osbyte

                ; (Don't bother storing the result anywhere. The
                ; previous owner is not going to get it back.)

                ; Run the loader.
                jmp loader1_base

unexpected_guid:
                brk
                .byte 255
                .text "Unexpected loader contents"
                .byte 0
                
xdigits: .text "0123456789ABCDEF"
                
read_pblock: .block
sector=80*16
                .byte 0
                .dword loader1_base
                .byte $08       ; read
                .byte 0         ; drive OR bits=0; sector# bits 16-21=0
                .byte >sector   ; sector# bits 8-15
                .byte <sector   ; sector# bits 0-7
                .byte 16        ; sector count
                .byte 0         ; unused
                .dword 0        ; data length (unused for reads)
                .endblock

expected_loader1_guid: .text loader1_guid
                
                .endsection

;-------------------------------------------------------------------------
