                .include "../submodules/beeb/include/beeb.s65"
                .include "shared_constants.s65"

*=$00
                .dsection fdload_zp
                .dsection zx02_zp
                .dsection zp
                .cerror *>$90

fdload_banked_version=true
fdload_rom_bank=7
                
*=loader1_base
                .dsection code
fdload_begin:
                .logical $b800
fdload_dest:
fdload_toc:
                .binary "../build/pics_disk/intermediates/toc.dat"
                .dsection fdload_init_code
fdload_working_code:
                .logical $900
                .dsection fdload_working_code
                .endlogical
                .cerror *>$bffd
*=$bffd
                jsr fdload.init
                .endlogical
fdload_code_end:
                
                .cerror *>loader1_base+loader1_size-16
*=loader1_base+loader1_size-16
                .text loader1_guid

;-------------------------------------------------------------------------

                .section zp
                .endsection

;-------------------------------------------------------------------------

                .section code
                .block
start:
                ldx #0
print_init_text_loop:
                lda init_text,x
                jsr oswrch
                inx
                cpx #size(init_text)
                bne print_init_text_loop

                ; copy fdload code into ROM
                jsr fdload_select_bank
                ldx #$08        ; 8 pages = 2 KB
                ldy #0
copy_fdload_loop:
lda_fdload_byte: lda fdload_begin,y
sta_fdload_byte: sta fdload_dest,y

                ; get rid of the original fdload code
                lda #0
                sta fdload_begin,y
                
                iny
                bne copy_fdload_loop
                inc lda_fdload_byte+2
                inc sta_fdload_byte+2
                dex
                bne copy_fdload_loop

                jsr fdload.one_time_init

                ; select some other ROM bank...
                lda #4
                sta $f4
                sta romsel

                lda acccon
                and #~acccon.d  ; display main memory
                ora #acccon.x   ; page in shadow memory
                sta acccon

loop:
                jsr fdload.load_compressed_file
                .word $3000
                .byte $0e
next_file_index:
                .byte $00

                lda #19
                jsr osbyte

                lda acccon
                eor #acccon.d|acccon.x
                sta acccon

                lda next_file_index
                inc a
                cmp fdload.toc_header.num_files
                bne +
                lda #0
+
                sta next_file_index
                
                jmp loop

init_text: .block
                .byte 22,129
                .byte 22,1
                .byte 23,1,0,0,0,0,0,0,0,0
                .endblock

                .endblock

fdload_select_bank:
                ldy romsel
                ldx #fdload_rom_bank
                stx $f4
                stx romsel
                rts

fdload_restore_bank:
                sty $f4
                sty romsel
                rts
                
                .endsection code

;-------------------------------------------------------------------------

                .include "fdload.s65"
                